grammar edu.cmu.sei.alisa.Alisa with org.eclipse.xtext.common.Terminals

generate alisa "http://www.cmu.edu/sei/alisa/Alisa"
//import "platform:/resource/org.osate.aadl2/model/aadl2.ecore" as aadl2
//import "http://aadl.info/AADL/2.0" as aadl2

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.osate.aadl2/model/aadl2.ecore" as aadl2


AlisaModel:
	RDAPackage | VerificationLibrary | RequirementDocument  
		| Organization |ExternalDocuments | AlisaConfiguration
	;

ContractualElement returns ContractualElement: Goal | Requirement ;

ReqDocContent: ContractualElement | DocumentSection;

AlisaDocument: RequirementDocument | RDAPackage;

//NamedElement returns aadl2::NamedElement :
//	AlisaElement
//;

AlisaElement :
Requirement| Goal  |RDAPackage | AlisaConfiguration | Category
|Stakeholder
| Organization | Goals | Requirements
|RequirementDocument | ExternalDocuments | VerificationLibrary
|VerificationActivity|VerificationResult;

AlisaNameSpace: 
	RequirementDocument | ExternalDocuments | VerificationLibrary
	|RDAPackage | Goals | Requirements | Organization | AlisaConfiguration
;

RequirementDocument:
	'requirement' 'document' name=ID
	(content += ReqDocContent)* 
	'end' 'requirement' 'document'
	';';
	
DocumentSection:
	'section' name =ID
		(content += ReqDocContent)* 
	'end' 'section' ';'
;


VerificationLibrary:
	'verification' 'library' name=ID
	content += (VerificationActivity | VerificationResult)*  
	'end' 'verification' 'library'
	';';
//
//ConstraintLibrary:
//	'constraint' 'library' name=ID
//	content += (Requirements)*  
//	'end' 'constraint' 'library'
//	';';

RDAPackage:
	'package' name = ID
	('imports' importedNamespace+=[AlisaNameSpace](',' importedNamespace+=[AlisaNameSpace])*';')?
	('with' importedNamespace+=[aadl2::AadlPackage|QPREF](',' importedNamespace+=[aadl2::AadlPackage|QPREF])*';')?
	content+=(Goals | Requirements  
	)*
	'end' 'package' ';';

Goals:
	'goals' name=ID 'for' goalTarget=[aadl2::NamedElement|QCLREF]
	(goals += Goal)*
	('issues' ':' issue+=ValueString (','issue+=ValueString)* ';')? 
	'end' 'goals'';'
	;
	

Goal:
	'goal' name=ID 
	(( 'for' target=RELREF)? //goalTarget=[aadl2::NamedElement|RELREF])?
	& ('category' ':' category+=[Category|CATREF](','category+=[Category|CATREF])*';')?
	& ('title' ':' title=ValueString ';')? 
	& ('description' ':' description=ValueString';')? 
	& ('assertion' ':' 	assert=ValueString ';')? 
	& ('rationale' ':' rationale=ValueString';')? 
	& ('issues' ':' issue+=ValueString (','issue+=ValueString)* ';')? 
	& ('see' ':' modelReference+=QCRELREF (',' modelReference+=QCRELREF)*';')? 
	& ('refines' ':' refinesReference+=[Goal|REQREF](',' refinesReference+=[Goal|REQREF])*';')? 
	& ('decomposes' ':' decomposesReference+=[Goal|REQREF](',' decomposesReference+=[Goal|REQREF])*';')? 
	& ('evolves' ':' evolvesReference+=[Goal|REQREF](',' evolvesReference+=[Goal|REQREF])*';')? 
	& ('conflicts' ':' conflictsReference+=[Goal|REQREF](',' conflictsReference+=[Goal|REQREF])*';')? 
	& ('stakeholder' ':' stakeholderReference+=[Stakeholder|RELREF] (',' stakeholderReference+=[Stakeholder|RELREF])*';')?
	& ('see' 'stakeholder' 'req' ':' stakeholderRequirementReference+=[Goal|REQREF] (',' stakeholderRequirementReference+=[Goal|REQREF])*';')?
	&('see' 'system' 'req' ':' systemRequirementReference+=[ContractualElement|REQREF] (',' systemRequirementReference+=[ContractualElement|REQREF])*';')?
	& ('see' 'document' ':' docReference+=[ExternalDocument|RELREF] (',' docReference+=[ExternalDocument|RELREF])*';')? 
	)
	 'end''goal'';';

Requirements:
	'requirements' name=ID 'for' reqTarget=[aadl2::NamedElement|QCLREF]//(reqtarget=[aadl2::Classifier|QCLREF]|('property'reqtarget=[aadl2::Property|QCLREF ])) 
	(reqs+=Requirement)*
	('issues' ':' issue+=ValueString (','issue+=ValueString)* ';')? 
	'end' 'requirements' ';';

//ContractCategory : 'requirement'|'assumption'|'input''assumption'|'guarantee'|'output''guarantee'|'invariant'|'constraint'|'design''constraint'|'development' 'requirement'|'system' 'requirement';
	
Requirement:
	'requirement'  name=ID   
	
	(( 'for' target=RELREF)? //target=[aadl2::NamedElement|RELREF])?
 	&('category' ':' category+=[Category|CATREF](',' category+=[Category|CATREF])*';')?
	&('title' ':' title=ValueString';')? 
	&('description' ':' description=ValueString';')? 
	&('assertion' ':' assert=ValueString';')? 
	&('rationale' ':' rationale=ValueString';')? 
	&('issues' ':' issue+=ValueString (','issue+=ValueString)* ';')? 
	&('see' ':' modelReference+=QCRELREF (',' modelReference+=QCRELREF)*';')? 
	&('see' 'goal' ':' goalReference+=[Goal] (',' goalReference+=[Goal])*';')?
	&('see' 'hazard' ':' hazardReference+=QNEREF (',' hazardReference+=QNEREF)*';')? // hazardReference+=[aadl2::NamedElement|QNEREF]
	&('refines' ':' refinesReference+=[Requirement|REQREF](',' refinesReference+=[Requirement|REQREF])*';')? 
	&('decomposes' ':' decomposesReference+=[Requirement|REQREF](',' decomposesReference+=[Requirement|REQREF])*';')? 
	&('evolves' ':' evolvesReference+=[Requirement|REQREF](',' evolvesReference+=[Requirement|REQREF])*';')? 
	&('verified' 'by' ':' verifiedBy+=(VerificationDecomposition)*)?
	&('see' 'stakeholder' 'req' ':' stakeholderRequirementReference+=[Goal|REQREF] (',' stakeholderRequirementReference+=[Goal|REQREF])*';')?
	&('see' 'system' 'req' ':' systemRequirementReference+=[ContractualElement|REQREF] (',' systemRequirementReference+=[ContractualElement|REQREF])*';')?
	&('see' 'document' ':' docReference+=XDocUri (',' docReference+=XDocUri)*';')? 
	)
	'end''requirement'';';


ExternalDocuments:
	'documents'
	name=ID 
	docs+=ExternalDocument ( docs+=ExternalDocument)*
	'end''documents' ';'
	
;


ExternalDocument:
	'document'
	name=ID 'see' ':' externalReference=DOCPATH ';'
	
;


XDocUri : 
	docPath=[ExternalDocument|RELREF] ( '#' docFragment=DOCFRAGMENT)?
;

AlisaConfiguration:
	'configuration' name = ID
	'categories'
	(category+= Category)*
	'end' 'categories' ';'
	'end' 'configuration'';'
;

Category :
	'category' name = ID ('extends' extends=[Category|CATREF])?';'
;


ValueString :
	// remove quotes from string in ValueConverter 
	STRING
;

/*
 * Stakeholder
 */
Stakeholder:
	'stakeholder' name=ID
	(('title' ':' title=ValueString ';')?
	&('description' ':' description=ValueString ';')?
	&('role' ':' role=ValueString ';')?
	&('email' ':' email=ValueString';')?
	&('phone' ':' phone=ValueString';')?)
	'end' 'stakeholder' ';'
;


Organization:
	'organization' name=ID 
	(stakeholder += Stakeholder)*
	'end' 'organization'';'
	;



VerificationDecomposition:
	element=([VerificationActivity|RELREF])|
	left=([VerificationActivity|RELREF]) type=('and'|'or') right=(VerificationDecomposition)
;



/*
 * Verification Activity
 */

VerificationActivity:
	'verification' 'activity' name=ID
	('title' ':' title=ValueString';')?
	('description' ':' description=ValueString';')?
	('method' ':' method=VerificationMethod';')?
//	('parameters' ':' verificationParameters+=([ElementReference])*)?
	('decomposed' 'to' ':' decomposedTo+=(VerificationDecomposition)*';')?
	('assigned' 'to' ':' assignedTo+=([Stakeholder|RELREF])*';')?
	'end' 'verification' 'activity' ';'
;
 

VerificationMethod:
	'manual' | 'automatic' | 'review' | 'testing' | 'analysis' | 'verification'
;


/*
 * Verification Result
 */

VerificationResult:
	'verification' 'result' name=ID 'of' referencedVerificationMethod+=([VerificationActivity])*
	('output' ':' title=ValueString';')?
	('logfile' ':' description=ValueString';')?
	('date' ':' method=ValueString';')?
	('state' ':' state=VerificationResultState';')?
	('status' ':' status=VerificationResultStatus';')?
	'end' 'verification' 'result' ';'
;

enum  VerificationResultState:
	SCHEDULED='scheduled' | COMPLETED='completed' | INPROGRESS='inprogress'
;

enum VerificationResultStatus:
	PASS='pass' | FAIL='fail' | UNKNOWN='unknown'
;

	// classifier reference
QCLREF:
	(ID '::')* ID ('.' ID)?;

	// package reference
QPREF:
	ID ('::' ID)*;

// error model element reference or other multi-dot path with or without qualifier
QNEREF:
	(ID '::')* ID ('.' ID)*;

//  dotted path property ref or property ref
QCRELREF:
	(QNEREF (PAREF)?)|PAREF
;

// dotted path as relative reference
RELREF:
	ID ('.' ID)* ;	

// dotted path as relative reference
CATREF:
	ID '.' ID ;	

// reference to property (association)	
PAREF: '#' ID ('::' ID)?;

// requirement or goal reference (just dotted)
REQREF: (ID '.')* ID ;

DOCPATH: ( ID '/')* ID '.' ID;

DOCFRAGMENT: ID ('.' ID)*;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

	//terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal ID:
	('a'..'z'
	| 'A'..'Z') (('_')? ('a'..'z'
	| 'A'..'Z'
	| '0'..'9'))*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;



